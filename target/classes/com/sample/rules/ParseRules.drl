//created on: 16 nov 2023
package com.rules

//list any import classes here.

import ontology.objetos.*;
import ontology.objetos.ondas.*;
import ontology.objetos.segmentos.*;
import ontology.objetos.intervalos.*;
import ontology.objetos.complejos.*;
//import ontology.onjetos.ondas.*

//declare any global variables here



rule "Crea SegmentoPR"
    //include attributes such as "salience" here...
    agenda-group "Ciclos"
    
    when
        $ondP: OndaP()
        $ondQ: OndaQ(numCiclo == $ondP.getNumCiclo())
    then
       	insert(new SegmentoPR($ondP.getFin(),$ondQ.getInicio(),$ondP.getNumCiclo()));
end

rule "Crea SegmentoST"
    //include attributes such as "salience" here...
    agenda-group "Ciclos"
    when
        $ondS: OndaS()
        $ondT: OndaT(numCiclo == $ondS.getNumCiclo())
    then
       	insert(new SegmentoST($ondS.getFin(),$ondT.getInicio(),$ondS.getNumCiclo()));
end

rule "Crea SegmentoTP"
    //include attributes such as "salience" here...
    agenda-group "Ciclos"
    when
        $ondT: OndaT()
        $ondP: OndaP(numCiclo-1 == $ondT.getNumCiclo())
    then
       	insert(new SegmentoTP($ondT.getFin(),$ondP.getInicio(),$ondT.getNumCiclo()));

end

rule "Crea Ritmo Cardiaco"
	agenda-group "Ciclos"
   when
    	$ondMin: Onda() // Patrón para seleccionar cualquier objeto Ejemplo
    	$ondMax: Onda()
    	not Onda(inicio < $ondMin.getInicio()) // No hay otro objeto con valorBajo menor
    	not Onda(fin > $ondMax.getFin()) // No hay otro objeto con valorAlto mayor
	then
    	// $ejemplo contiene el objeto con el valor más bajo de valorBajo y el valor más alto de valorAlto
    	int rpm = (int)(Math.ceil((60000.0/(($ondMax.getFin() - $ondMin.getInicio())/$ondMax.getNumCiclo()))));
    	insert(new RitmoCardiaco(rpm));

end

rule "Crea Complejo QRS"
	agenda-group "Ciclos"
    when
		$ondQ: OndaQ() // Patrón para seleccionar cualquier objeto Ejemplo
    	$ondR: OndaR(numCiclo == $ondQ.getNumCiclo())
    	$ondS: OndaS(numCiclo == $ondQ.getNumCiclo())
    then
       	insert(new ComplejoQRS($ondQ,$ondR,$ondS));
       

end  
rule "Crea intervaloPR"
agenda-group "Ciclos"
    when
		$segPR: SegmentoPR() // Patrón para seleccionar cualquier objeto Ejemplo
    	$ondP: OndaP(numCiclo == $segPR.getNumCiclo())
    then
       	insert(new IntervaloPR($ondP,$segPR));
       

end

rule "Crea intervalosQT"
    //include attributes such as "salience" here...
    agenda-group "Ciclos"
    when
		$segST: SegmentoST() // Patrón para seleccionar cualquier objeto Ejemplo
    	$ondT: OndaT(numCiclo == $segST.getNumCiclo())
    then
       	insert(new IntervaloQT($ondT,$segST));
end


       

